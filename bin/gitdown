#! /usr/bin/perl
#   gitdoc - a tool for writing github-hosted documents.
#
#   See README.txt for details.
#
#   Copyright (c) 1996-2010 iMatix Corporation
#
#   This is free software; you can redistribute it and/or modify it under the
#   terms of the GNU General Public License as published by the Free Software
#   Foundation; either version 3 of the License, or (at your option) any later
#   version.
#
#   This software is distributed in the hope that it will be useful, but
#   WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABIL-
#   ITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public
#   License for more details.
#
#   You should have received a copy of the GNU General Public License along
#   with this program. If not, see <http://www.gnu.org/licenses/>.
#

use strict;
use File::Basename;
use Getopt::Long;

# push gitdown path to include for modules
my $path = dirname(__FILE__);
push (@INC, $path);

#   Uses the Perl SFL modules from htmlpp
require 'sflcvdp.pl';                   #   SFL date picture formatting

my $version = "2010.10.09";
my $ditaa = "java -jar $path/ditaa0_9.jar";

my $to_local = 0;
my $nofunc_mode = 0;
GetOptions (
    'help' => sub { &die_usage },
    'nofunc-mode' => \$nofunc_mode,
    'to-local' => \$to_local,
);

my $input = $ARGV [0];
my $self = $input;
$self =~ s/\.[^.]*$//;
my (@inbuf, $output);

# Parse and validate provided filename
if ($#ARGV == 0) {
    $output = "$self.md";
}
elsif ($#ARGV == 1) {
    $output = $ARGV [1];
}
else {
    &die_usage;
}

die "Input should be a .txt file, not a .md file\n"
    if $input eq $output;

#   Read input file into @inbuf array
die "Can't read $input: $!"
    unless open (INPUT, $input);
while (<INPUT>) {
    chop;
    push @inbuf, $_;
}
close (INPUT);
&insert_anchors;

die "Can't create images.html: $!"
    unless open (IMAGES, ">images.html");

if ($output eq '-') {
    *OUTPUT = *STDOUT;
} else {
    die "Can't create $output: $!"
        unless open (OUTPUT, ">$output");
}

# all messages to stderr
*STDOUT = *STDERR;

writeln_images ("<html>");

# set defaults for symbols
my %symbols = {};
$symbols {"INPUT"}  = $input;
$symbols {"SELF"}   = $self;
$symbols {"OUTPUT"} = $output;
$symbols {"PREBRANCH"} = "raw";
$symbols {"BRANCH"} = "master";

my ($EOD, $prev, %subsset, $tocl);
my $line = 0;
my $diagram = 0;
my $have_errors = 0;
my $imgpath = $to_local ? "images/" : &gen_imgpath;

while ($line < @inbuf) {
    $_ = $inbuf [$line++];
    if (/^\./) {
        #   Process directive
        if (/^\.set\s+(\w+)=(.*)\s*/) {
            $symbols {$1} = $2;

            # probably quicker to rebuild image path on each symbol definition
            # than to check and see if it's changed
            $imgpath = &gen_imgpath unless $to_local;
        }
        elsif (/^\.toc(\s+([1-9]))?/) {
            #   Determine top level in text after .toc
            if ($2) {
                $tocl = $2;
            }
            else {
                $tocl = 4;
                for (my $scan = $line; $scan < @inbuf; $scan++) {
                    $_ = $inbuf [$scan];
                    if (/<A name="(toc([0-9])-.*)" title=\"(.*)\"/) {
                        $tocl = $2 if $2 < $tocl;
                    }
                }
            }
            $prev = "";
            for (my $scan = $line; $scan < @inbuf; $scan++) {
                $_ = $inbuf [$scan];
                if (/<A name="(toc([0-9])-.*)" title=\"(.*)\"/) {
                    #   Top level items at left, others along same line...
                    if ($2 > $tocl) {
                        writeln ("&emsp;<a href=\"#$1\">$3</a>");
                    }
                    else {
                        writeln ("\n**<a href=\"#$1\">$3</a>**");
                    }
                }
            }
        }
        elsif (/^\.sub\s+([^=]+)=(.*)\s*/) {
            $subsset {$1} = $2;
        }
        elsif (/^\.\-/) {
            #   Comment, ignore
        }
        elsif (/^\.end/) {
            writeln ("(More coming soon...)");
            $EOD = 1;       #   Stop output here
        }
        else {
            print "Illegal directive $_ at line $.\n";
        }
    }
    elsif ($_ eq "[diagram]") {
        #   Shunt diagram into temporary HTML file for Ditaa
        $diagram = $diagram + 1;
        $symbols {"DIAGRAM"} = $diagram;
        writeln_images ("<pre class=\"textdiagram\" id=\"$self\_$diagram\">");
        while ($line < @inbuf) {
            $_ = $inbuf [$line++];
            last if /\[\/diagram\]/;
            s/#/$diagram/;
            writeln_images ($_);
        }
        writeln_images ("</pre>");
        writeln ("<center>");
        writeln ("<img src=\"$imgpath$self\_$diagram.png\" alt=\"$diagram\">");
        writeln ("</center>");
    }
    else {
        #   Normal text
        writeln ($_);
    }
}
writeln_images ("</html>");
close (IMAGES);
close (OUTPUT);

system ("rm -f images/$self\_*");
if ($diagram) {
    system ("$ditaa images.html -o -h -E output.html 1>&2");
    #   Need to trim twice for reasons I don't care to explore
    system ("mogrify -trim images/$self\_*.png");
    system ("mogrify -trim images/$self\_*.png");
}
system ("rm -f output.html images.html");
exit ($have_errors);

sub die_usage {
    print "Syntax: gitdoc [options] textfilename\n";
    print "  Version $version\n\n";
    print "  --help		display this message\n";
    print "  --to_local		override symbols and make images be relative\n";
    print "			to local directory\n";
    print "  --nofunc-mode		disable intrinsic function errors\n";
    print "\n";

    exit (1);
}

sub gen_imgpath {
    my $giturl = $symbols{GIT}.'/';
    # allow double slash after : for url
    $giturl =~ s|(?<!:)/+|/|g;

    my $subpath = "$symbols{PREBRANCH}/$symbols{BRANCH}/$symbols{SUBDIR}/images/";
    # remove starting /
    $subpath =~ s|^/||;
    # remove double /
    $subpath =~ s|/+|/|g;
    $giturl .= $subpath;
    return ($giturl);
}

#   Writes $_ to OUTPUT after expanding all symbols

sub writeln {
    my ($_) = @_;
    #   Don't expand symbols in code blocks
    $_ = expand_symbols ($_) unless /^    /;
    foreach my $token (keys %subsset) {
        s/$token/$subsset{$token}/g;
    }
    print OUTPUT "$_\n" unless $EOD;
}

sub writeln_images {
    my ($_) = @_;
    $_ = expand_symbols ($_);
    foreach my $token (keys %subsset) {
        s/$token/$subsset{$token}/g;
    }
    print IMAGES "$_\n" unless $EOD;
}


#   Insert anchors into text before each header

sub insert_anchors {
    $prev = "";
    for ($line = 0; $line < @inbuf; $line++) {
        $prev = $_;
        $_ = $inbuf [$line];

        if (/^===/) {
            splice @inbuf, $line - 1, 0, "<A name=\"toc1-$line\" title=\"$prev\" />";
            $line++;
        }
        elsif (/^---/) {
            splice @inbuf, $line - 1, 0, "<A name=\"toc2-$line\" title=\"$prev\" />";
            $line++;
        }
        elsif (/^# /) {
            splice @inbuf, $line, 0, "<A name=\"toc1-$line\" title=\"$'\" />";
            $line++;
        }
        elsif (/^## /) {
            splice @inbuf, $line, 0, "<A name=\"toc2-$line\" title=\"$'\" />";
            $line++;
        }
        elsif (/^### /) {
            splice @inbuf, $line, 0, "<A name=\"toc3-$line\" title=\"$'\" />";
            $line++;
        }
        elsif (/^#### /) {
            splice @inbuf, $line, 0, "<A name=\"toc4-$line\" title=\"$'\" />";
            $line++;
        }
    }
}


#-- Symbol expansion code, taken from htmlpp

#   Recursively expand symbols like this (and in this order):
#
#   $(xxx)                  - value of variable
#   $(xxx?zzz)              - value of variable, or zzz if undefined
#   %(text?zzz)             - value of environment variable, or zzz if undef
#   &abc(text)              - intrinsic function with arguments
#
sub expand_symbols {
    my ($_) = @_;
    my ($before, $match, $after, $expr);

    return unless ($_);                 #   Quit if inbuf string is empty

    for (;;) {
        #   Force expansion from end of string first, so things like
        #   $(xxx?$(yyy)) work properly.
        if (/[\$%]\(/ || /\&([a-z_]+)\s*\(/i) {
            $before = $`;
            $match  = $&;
            $after  = expand_symbols ($');
            $_ = $before.$match.$after;
        }
        #   $(xxx)
        if (/\$\(([A-Za-z0-9-_\.]+)\)/) {
            $_ = $`.&valueof ($1).$';
        }
        #   $(xxx?zzz)
        elsif (/\$\(([A-Za-z0-9-_\.]+)\?([^)\$]*)\)/) {
            $_ = $`.&valueof ($1, $2).$';
        }
        #   %(text)
        elsif (/\%\(([^\)]+)\)/) {
            $_ = $`.$ENV {$1}.$';
        }
        #   %(text?zzz)
        elsif (/\%\(([^\)]+)\?([^)\$]*)\)/) {
            $_ = $`.($ENV {$1}? $ENV {$1}: $2).$';
        }
        #   &abc(text)
        elsif (/\&([a-z_]+)\s*\(([^\)]*)\)/i) {
            my $funct = $1;
            my $args = $2;
            $before = $`;
            $after = $';
            $args =~ s/\\/\\\\/g;
            $_ = eval ("&intrinsic_$funct ($args)");
            $_ = $before.$_.$after;
            if ($@) {                   #   Syntax error in Perl statement?
                &error ("$funct is not a valid intrinsic function")
                    unless $nofunc_mode;
                last;
            }
        }
        else {
            last;
        }
    }
    return $_;
}


#   Subroutine returns the value of the specified symbol; it issues a
#   warning message and returns 'UNDEF' if the symbol is not defined
#   and the default value is empty.
#
sub valueof {
    my ($symbol, $default) = @_;     #   Argument is symbol name
    my ($return);                    #   Returned value
    my ($langed_symbol);             #   Language-dependent symbol

    if (defined ($symbols {$symbol})) {
        $return = $symbols {$symbol};
        return $return;
    }
    elsif (defined ($default)) {
        return ($default);
    }
    &error ("$_");
    &error ("($.) undefined symbol \"$symbol\"");
    our $default_warning == 1 || do {
        &error ("I: Use \$($symbol?default) for default values.");
        $default_warning = 1;
    };
    $symbols {$symbol} = "UNDEF";
    return $symbols {$symbol};
}


#   INTRINSIC FUNCTIONS
#
#   time()                     - Format current time as hh:mm:ss
#   date()                     - Return current date value
#   date("picture")            - Format current date using picture
#   date("picture", date, lc)  - Format specified date using picture & language
#   week_day([date])           - Get day of week, 0=Sunday to 6=Saturday
#   year_week([date])          - Get week of year, 1 is first full week
#   julian_date([date])        - Get Julian date for date
#   lillian_date([date])       - Get Lillian date for date
#   date_to_days(date)         - Convert yyyymmdd to Lillian date
#   days_to_date(days)         - Convert Lillian date to yyyymmdd
#   future_date(days[,date])   - Calculate a future date
#   past_date(days[,date])     - Calculate a past date
#   date_diff(date1[,date2])   - Calculate date1 - date2
#   image_height("image.ext")  - Get image height (GIF, JPEG)
#   image_width("image.ext")   - Get image width (GIF, JPEG)
#   file_size("filename",arg)  - Get size of file: optional arg K or M
#   file_date("filename")      - Get date of file
#   file_time("filename")      - Get time of file as hh:mm:ss
#   normalise("filename")      - Normalise filename to UNIX format
#   system("command")          - Call a system utility
#   lower("string")            - Convert string to lower case
#   upper("string")            - Convert string to upper case
#

sub intrinsic_date {
    my ($picture, $value, $language) = @_;
    $value = &date_now unless $value;
    $language = $symbols{LANG} unless $language;
    if ($picture) {
        return (&conv_date_pict ($value, $picture, $language));
    }
    else {
        return ($value);
    }
}

sub intrinsic_time {
    my ($sec, $min, $hour, $day, $month, $year) = localtime;
    return (sprintf ("%2d:%02d:%02d", $hour, $min, $sec));
}

sub intrinsic_week_day {
    return (&day_of_week ($_ [0]? $_ [0]: &date_now));
}

sub intrinsic_year_week {
    return (&week_of_year ($_ [0]? $_ [0]: &date_now));
}

sub intrinsic_julian_date {
    return (&julian_date ($_ [0]? $_ [0]: &date_now));
}

sub intrinsic_lillian_date {
    return (&date_to_days ($_ [0]? $_ [0]: &date_now));
}

sub intrinsic_date_to_days {
    return (&date_to_days ($_ [0]));
}

sub intrinsic_days_to_date {
    return (&days_to_date ($_ [0]));
}

sub intrinsic_future_date {
    my ($date) = &future_date ($_ [1], 0, $_ [0], 0);
    return ($date);
}

sub intrinsic_past_date {
    my ($date) = &past_date ($_ [1], 0, $_ [0], 0);
    return ($date);
}

sub intrinsic_date_diff {
    my ($date1, $date2) = @_;
    $date1 = &date_now unless $date1;
    $date2 = &date_now unless $date2;
    my ($days) = &date_diff ($date1, 0, $date2, 0);
    return ($days);
}

sub intrinsic_image_height {
    my ($filename) = @_;
    if (! -e $filename) {
        &error ("($.) file not found: \"$filename\"");
    }
    else {
        return (&image_height ($filename));
    }
}

sub intrinsic_image_width {
    my ($filename) = @_;
    if (! -e $filename) {
        &error ("($.) file not found: \"$filename\"");
    }
    else {
        return (&image_width ($filename));
    }
}

sub intrinsic_file_size {
    my ($filename, $arg) = @_;
    my ($size) = (stat ($filename)) [7];

    if (! -e $filename) {
        &error ("($.) file not found: \"$filename\"");
    }
    elsif ($arg eq "K") {
        $size /= 1024;
    }
    elsif ($arg eq "M") {
        $size /= 1048576;
    }
    return (int ($size));
}

sub intrinsic_file_date {
    my ($filename) = @_;
    if (! -e $filename) {
        &error ("($.) file not found: \"$filename\"");
    }
    else {
        my ($mtime) = (stat ($filename)) [9];
        my ($sec,$min,$hour,$mday,$mon,$year) = localtime ($mtime);
        return (($year + 1900) * 10000 + ($mon + 1) * 100 + $mday);
    }
}

sub intrinsic_file_time {
    my ($filename) = @_;
    if (! -e $filename) {
        &error ("($.) file not found: \"$filename\"");
    }
    else {
        my ($mtime) = (stat ($filename)) [9];
        my ($sec,$min,$hour,$mday,$mon,$year) = localtime ($mtime);
        return (sprintf ("%2d:%02d:%02d", $hour, $min, $sec));
    }
}

sub intrinsic_normalise {
    my ($_) = @_;                    #   Get filename argument
    s/\\/\//g;                          #   Replace DOS-style \ by /
    s/\s/_/g;                           #   Replace white space by _
    return ($_);
}

sub intrinsic_system {
    my ($_) = `@_`;

    #   Return all but the last character, which should be a newline
    chop;
    return ($_);
}

sub intrinsic_lower {
    my ($_) = @_;                    #   Get filename argument
    tr/A-Z/a-z/;
    return ($_);
}

sub intrinsic_upper {
    my ($_) = @_;                    #   Get filename argument
    tr/a-z/A-Z/;
    return ($_);
}

sub error {
    ($_) = @_;                          #   Get argument
    print STDERR "E: $_\n";
    $have_errors = 1;                   #   We have 1 or more errors
}
